include "../std/std.ttc"

def width = 80
def height = 40
def max_iteration = 100
def sample_size = 10
def palette = " .,-~:;=!*#$@"

def scale(x, xmin, xmax, min, max) = min + (max - min) * (xmax - x) / (xmax - xmin)
def clamp(x, min, max) = if [x < min] min else if [x > max] max else x

def main(?) = (
    for [0, height] -> j (
        for [0, width] -> i (
            def t = 0
            for [0, sample_size] -> sj (
                for [0, sample_size] -> si (
                    def x0 = -scale(i, 0, width, -0.5, 2.0)
                    def y0 = scale(j, 0, height, -1.5, 1.5)
                    def x = 0
                    def y = 0
                    def iteration = 0
                    while [x * x + y * y <= 2 * 2 && iteration < max_iteration] (
                        def xtemp = x * x - y * y + x0
                        y = 2 * x * y + y0
                        x = xtemp
                        iteration += 1
                    )
                    t += iteration / max_iteration
                )
            )
            def color = palette[clamp(12 * t / (sample_size * sample_size), 0, 12)]
            putchar(color)
        )
        putchar('\n')
    )
    0
)
